load("@container_structure_test//:defs.bzl", "container_structure_test")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_doc_test", "rust_library", "rust_test")

rust_library(
    name = "runproc_rs",
    srcs = glob(["*.rs"]),
    crate_name = "runproc",
    crate_root = "runproc.rs",
    # will not work as expected on windows
    target_compatible_with = select({
        "@platforms//os:linux": [],
        "@platforms//os:macos": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    visibility = ["//visibility:public"],
    deps = [
        "//process/fsutil:fsutil_rs",
        "@crates//:clap",
        "@crates//:futures",
        "@crates//:humantime",
        "@crates//:libc",
        "@crates//:thiserror",
        "@crates//:tokio",
        "@crates//:tracing",
    ],
)

rust_binary(
    name = "runproc",
    srcs = ["cmd/runproc.rs"],
    visibility = ["//visibility:public"],
    deps = [
        ":runproc_rs",
        "@crates//:anyhow",
        "@crates//:tokio",
        "@crates//:tracing",
        "@crates//:tracing-subscriber",
    ],
)

rust_test(
    name = "runproc_rs_lib_test",
    size = "small",
    crate = ":runproc_rs",
    deps = [
        "//testing:testing_rs",
    ],
)

rust_doc_test(
    name = "runproc_rs_doc_test",
    size = "small",
    crate = ":runproc_rs",
)

cc_binary(
    name = "orphan",
    srcs = ["cmd/orphan.c"],
)

cc_binary(
    name = "zombie",
    srcs = ["cmd/zombie.c"],
)

pkg_tar(
    name = "tar",
    srcs = [
        ":orphan",
        ":runproc",
        ":zombie",
    ],
)

oci_image(
    name = "image",
    base = "@distroless_cc_debian12_nonroot",
    entrypoint = ["/runproc"],
    target_compatible_with = [
        "@platforms//os:linux",
    ],
    tars = [
        ":tar",
    ],
)

# To load on macOS:
#
#     devcontainer exec --workspace-folder $(bazel info workspace) bazel run //process/runproc:load
oci_load(
    name = "load",
    image = ":image",
    repo_tags = [package_name() + ":image"],
)

container_structure_test(
    name = "container_structure_test",
    configs = ["testdata/runproc.yaml"],
    image = ":image",
    target_compatible_with = [
        "@platforms//os:linux",
    ],
)
