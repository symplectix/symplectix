load("@rules_foreign_cc//foreign_cc:defs.bzl", "cmake")

filegroup(
    name = "all_srcs",
    srcs = glob(
        ["**"],
        exclude = [
            "ci/**",
            "docs/**",
            "examples/**",
            "fuzzers/**",
            "tests/**",
            "*.bazel",
        ],
    ),
)

_CACHE_ENTRIES = {
    "BUILD_CLI": "off",
    "BUILD_CLAR": "off",
    "BUILD_EXAMPLES": "off",
    "BUILD_FUZZERS": "off",
    "BUILD_SHARED_LIBS": "off",
    "BUILD_TESTS": "off",
    "USE_SSH": "on",
    "USE_HTTPS": "on",
    "USE_HTTP_PARSER": "builtin",
    "REGEX_BACKEND": "builtin",
    "EMBED_SSH_PATH": "$(location @libssh2//:libssh2)",
    "CMAKE_FIND_DEBUG_MODE": "on",
    # https://cmake.org/cmake/help/latest/variable/CMAKE_PREFIX_PATH.html
    "CMAKE_PREFIX_PATH": "$EXT_BUILD_DEPS/openssl;$EXT_BUILD_DEPS/libssh2;$EXT_BUILD_DEPS/zlib;${CMAKE_PREFIX_PATH:-}",
    "CMAKE_BUILD_TYPE": "Release",
}

_LINUX_CACHE_ENTRIES = dict(_CACHE_ENTRIES.items() + {
    "CMAKE_C_FLAGS": "${CMAKE_C_FLAGS:-} -fPIC",
}.items())

cmake(
    name = "libgit2",
    cache_entries = select({
        "@platforms//os:linux": _LINUX_CACHE_ENTRIES,
        "//conditions:default": _CACHE_ENTRIES,
    }),
    lib_source = ":all_srcs",
    out_static_libs = select({
        "@platforms//os:windows": ["git2.lib"],
        "//conditions:default": ["libgit2.a"],
    }),
    visibility = ["//visibility:public"],
    deps = [
        "@libssh2",
        "@openssl",
        "@zlib",
    ],
)
